/* * PROGRAMA: CAMAR3$                                              */
/* ***--FORMALISTICA CHEQUES CANJE--*                               */
             PGM

             DCL        VAR(&RCDNBR) TYPE(*DEC) LEN(010 0)
             DCL        VAR(&TEST) TYPE(*CHAR) LEN(001)
             DCL        VAR(&TRUE) TYPE(*CHAR) LEN(001) VALUE('1')
             DCL        VAR(&USER) TYPE(*CHAR) LEN(010)
             DCL        VAR(&FILE) TYPE(*CHAR) LEN(010)
             DCL        VAR(&INDEX) TYPE(*CHAR) LEN(010)
             DCL        VAR(&CMD) TYPE(*CHAR) LEN(200)

             RTVJOBA    USER(&USER)

             RTVMBRD    FILE(*LIBL/CAMAG0$) NBRCURRCD(&RCDNBR)

             IF         COND(*NOT (&RCDNBR *EQ 0)) THEN(DO)
             PAUSE      MSG('YA NO SE PUEDE HACER FORMALISTICA')
             GOTO ALFIN
             ENDDO
             CALL       VERAGECM$ PARM(&USER)
             CHGVAR     VAR(&FILE) VALUE(%SST(*LDA 037 008))
             CHGVAR     VAR(&INDEX) VALUE(%SST(*LDA 037 005) *TCAT +
                          'CUS')
             CHGVAR     VAR(&TEST) VALUE(&TRUE)
             CHKOBJ     OBJ(&INDEX) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(*NOT (&TEST *EQ &TRUE)) THEN(DO)

/* // IFF DATAF1- (%SST(*LDA 037 005)) CUS BLDINDEX  (%SST(*LDA +
                          037 005)) CU$,24,7, (%SST(*LDA 037 008)) +
                          ,,DUPKEY */
 /*          RUNSQL     SQL('SET SCHEMA QS36F')     */

             CHGVAR     VAR(&CMD) VALUE('CREATE INDEX ' *CAT +
                          %TRIM(&INDEX) *CAT ' ON ' *CAT +
                          %TRIM(&FILE) *CAT '(CAST(SUBSTRING(F00002 +
                          FROM 10 FOR 7) AS CHAR(7)) AS K00002 ASC) +
                          RCDFMT R' *CAT %TRIM(&FILE) *CAT ' ADD +
                          ALL COLUMNS')
             RUNSQL     SQL(&CMD) COMMIT(*NC)
             ENDDO

             CHGDTAARA  DTAARA(*LDA (1 10)) VALUE(&USER)
             OVRDSPF    FILE(PCAMAR) TOFILE(ONLINE/CAMAR3FM)
             OVRDBF     FILE(CAMINT) TOFILE(&INDEX)
             CALL       PGM(ONLINEC/CAMAR3NW)
             DLTOVR     FILE(*ALL)
/* * Agregado para truncamiento                                     */
             CALL       PGM(ONLINE/NCHMM912$)
 ALFIN:

             ENDPGM
