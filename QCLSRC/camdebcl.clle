/* **************************************************************** */
/*  JUNTA LOS ARCHIVOS CAMINT Y CAMIDL EN CAMAGO 0 CAMAGX           */
/*  DEBITA EN CUENTA CHEQUES CANJE/IMPRIME LISTADO P/ AUTORIZACION  */
/*  MODIF.: 1- SACAR PROG NL8BU001 Y NL8BU002C DEL PROCESO.         */
/*          2- LLEVAMOS A CONTROLM AMBOS PROGRAMAS                  */
/*          3- SACAMOS SIGNOFF DEL PROCESO                          */
/*          ZPH/JUL/2022                                            */
/* **************************************************************** */
             PGM

             DCL        VAR(&DATE) TYPE(*CHAR) LEN(006)
             DCL        VAR(&JOBA) TYPE(*CHAR) LEN(032)
             DCL        VAR(&PM07) TYPE(*CHAR) LEN(002)
             DCL        VAR(&RCDNBR) TYPE(*DEC) LEN(010)
             DCL        VAR(&TEST) TYPE(*CHAR) LEN(001)
             DCL        VAR(&TRUE) TYPE(*CHAR) LEN(001) VALUE('1')
             DCL        VAR(&USER) TYPE(*CHAR) LEN(010)
             DCL        VAR(&WORK) TYPE(*CHAR) LEN(032)
             DCL        VAR(&WSID) TYPE(*CHAR) LEN(002)

             RTVJOBA    USER(&USER)
             RTVJOBA    JOB(&JOBA)
             CHGVAR     &WSID &JOBA
             CHGDTAARA  DTAARA(*LDA (1 31)) VALUE(&WSID *TCAT &USER +
                          *TCAT '      ')
             RTVJOBA    DATE(&DATE)
             RTVMBRD    FILE(*LIBL/CAMINT) NBRCURRCD(&RCDNBR)
             IF         (&RCDNBR *EQ 0) DO
             SNDPGMMSG  MSG('TODAVIA NO SE TOMARON DATOS DE +
                          CHEQUES-MATRIZ') TOPGMQ(*EXT) MSGTYPE(*INQ)
             GOTO       CMDLBL(ALFIN)
             ENDDO
             CHGJOB     SWS(00000000)
             CALL       PGM(ONLINE/NCHM915L)
             IF         COND(%SWITCH(1XXXXXXX)) THEN(DO)
             SNDPGMMSG  MSG('TODAVIA NO SE TOMARON DATOS DE +
                          CHEQUES-CDE') TOPGMQ(*EXT) MSGTYPE(*INQ)
             GOTO ALFIN
             ENDDO
 CDE1:
             CHGJOB     SWS(00000000)
             CALL       PGM(ONLINE/NCHM916L)
             IF         (%SWITCH(1XXXXXXX)) DO
             SNDPGMMSG  MSG('TODAVIA NO SE TOMARON DATOS DE +
                          CHEQUES-ENCARNACION') TOPGMQ(*EXT) +
                          MSGTYPE(*INQ)
             GOTO       CMDLBL(ALFIN)
             ENDDO
             CHGJOB     SWS(00000000)
             CALL       PGM(GLOBAL/RTVDTAARAC) PARM(ESTAFOR)
             IF         COND((%SST(*LDA 311 001)) *EQ L) THEN(DO)
             SNDPGMMSG  MSG('*** RETOMAREMOS EL PROCESO DE +
                          FORMALISTICA ***') TOPGMQ(*EXT) MSGTYPE(*INQ)
             CHGDTAARA  DTAARA(ONLINE/ESTAFOR (1 1)) VALUE('L')
             GOTO ARRIBA
             ENDDO
             IF         COND((%SST(*LDA 311 001)) *EQ M) THEN(DO)
             SNDPGMMSG  MSG('*** PROCESO YA EJECUTADO EN LA FECHA +
                          ****') TOPGMQ(*EXT) MSGTYPE(*INQ)
             GOTO ALFIN
             ENDDO
             IF         COND((%SST(*LDA 311 001)) *EQ K) THEN(DO)
             SNDPGMMSG  MSG('*** RETOMAREMOS CAMAGE ****') +
                          TOPGMQ(*EXT) MSGTYPE(*INQ)
             CHGDTAARA  DTAARA(ONLINE/ESTAFOR (1 1)) VALUE('K')
             GOTO       CMDLBL(REPROZ)
             ENDDO
/* *****************************************************            */
/*  CONTROL TIPO DE CUENTA EN LOS ARCHIVOS DE CLEARING *            */
/* *****************************************************            */
             CHGJOB     SWS(00000000)
             SNDPGMMSG  MSG('CONTROLA TIPO DE CUENTA EN CASA +
                          MATRIZ') TOPGMQ(*EXT) MSGTYPE(*COMP)
             CHGJOB     SWS(00000000)
             CALL       ONLINE/NCHM923L
             IF         COND(%SWITCH(1XXXXXXX)) THEN(DO)
             SNDPGMMSG  MSG('HAY UN TIPO DE CUENTA NO VALIDO EN EL +
                          ARCHIVO DE MATRIZ') TOPGMQ(*EXT) +
                          MSGTYPE(*COMP)
             SNDPGMMSG  MSG('EL PROCESO NO PUEDE CONTINUAR') +
                          TOPGMQ(*EXT) MSGTYPE(*INQ)
             RETURN
             ENDDO
             SNDPGMMSG  MSG('CONTROLA TIPO DE CUENTA EN CDE') +
                          TOPGMQ(*EXT) MSGTYPE(*COMP)
             OVRDBF     FILE(CAMINT) TOFILE(CAMIDL)
             CALL       ONLINE/NCHM923L
             IF         COND(%SWITCH(1XXXXXXX)) THEN(DO)
             SNDPGMMSG  MSG('HAY UN TIPO DE CUENTA NO VALIDO EN EL +
                          ARCHIVO DE CDE') TOPGMQ(*EXT) MSGTYPE(*COMP)
             SNDPGMMSG  MSG('EL PROCESO NO PUEDE CONTINUAR') +
                          TOPGMQ(*EXT) MSGTYPE(*INQ)
             RETURN
             ENDDO
             DLTOVR     FILE(CAMINT)
 CDE2:
             SNDPGMMSG  MSG('CONTROLA TIPO DE CUENTA EN +
                          ENCARNACION ') TOPGMQ(*EXT) MSGTYPE(*COMP)
             OVRDBF     FILE(CAMINT) TOFILE(CAMIEN)
             CALL       PGM(ONLINE/NCHM923L)
             IF         COND(%SWITCH(1XXXXXXX)) THEN(DO)
             SNDPGMMSG  MSG('HAY UN TIPO DE CUENTA NO VALIDO EN EL +
                          ARCHIVO DE ENCARNACION') TOPGMQ(*EXT) +
                          MSGTYPE(*COMP)
             SNDPGMMSG  MSG('EL PROCESO NO PUEDE CONTINUAR') +
                          TOPGMQ(*EXT) MSGTYPE(*INQ)
             RETURN
             ENDDO
             DLTOVR     CAMINT
             SNDPGMMSG  MSG('CONTROLA TIPO DE CUENTA EN CAMARA +
                          INTERNA') TOPGMQ(*EXT) MSGTYPE(*COMP)
             OVRDBF     CAMINT CAMINTCI
             CALL       ONLINE/NCHM923L
             IF         (%SWITCH(1XXXXXXX)) DO
             SNDPGMMSG  MSG('HAY UN TIPO DE CUENTA NO VALIDO EN EL +
                          ARCHIVO DE CAM. INTERNA') TOPGMQ(*EXT) +
                          MSGTYPE(*COMP)
             SNDPGMMSG  MSG('EL PROCESO NO PUEDE CONTINUAR') +
                          TOPGMQ(*EXT) MSGTYPE(*INQ)
             RETURN
             ENDDO
             DLTOVR     CAMINT
/* ******************** HASTA AQUI CONTROL DE TIPO DE  cuenta ************************** */
 NOCONT:
             CALL       ONLINE/NCHM917L
             IF         COND(%SWITCH(1XXXXXXX)) THEN(DO)
             SNDPGMMSG  MSG('NO SE HIZO PROCESO DE ENVASS O TUVO UN +
                          ERROR, VERIFIQUE POR FAVOR') TOPGMQ(*EXT) +
                          MSGTYPE(*COMP)
             SNDPGMMSG  MSG('EL PROCESO PUEDE CONTINUAR') +
                          TOPGMQ(*EXT) MSGTYPE(*INQ)
             ENDDO
             CHGJOB     SWS(00000000)
             CALL       PGM(ONLINE/NMC0001CL)
             IF         (%SWITCH(1XXXXXXX)) DO
             SNDPGMMSG  MSG('EXISTEN CHEQ. DUPLICADOS, VERIFIQUE SU +
                          LISTADO') TOPGMQ(*EXT) MSGTYPE(*INQ)
             GOTO ALFIN
             ENDDO

             ALCOBJ     OBJ((*LIBL/CAMAR3  *PGM *EXCL))
             MONMSG     MSGID(CPF1002) EXEC(DO)
             SNDPGMMSG  MSG('SE ESTA HACIENDO FORMALISTICA, +
                          INTENTELO MAS TARDE') TOPGMQ(*EXT) +
                          MSGTYPE(*INQ)
             RETURN
             ENDDO
             SNDPGMMSG  MSG('VERIFIQUE CON OTRA AGECIA QUE TIENE +
                          SU PROPIA CAMARA COMPENSADORA SI ') +
                          TOPGMQ(*EXT) MSGTYPE(*COMP)
             SNDPGMMSG  MSG('YA TERMINARON DE GRABAR SUS CHEQUES +
                          QUE SERAN DEBITADOS EN ESTE PROCESO') +
                          TOPGMQ(*EXT) MSGTYPE(*COMP)
             SNDPGMMSG  MSG('PRESTE MUCHA ATENCION, MARQUE 0 PARA +
                          CONTINUAR') TOPGMQ(*EXT) MSGTYPE(*INQ)
             ALCOBJ     OBJ((QS36F/SOBREGIROS *FILE *EXCL))
/* *                                                                */
             CHGVAR     &TEST (&TRUE)
             CHKOBJ     OBJ(CAMINT) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         (*NOT (&TEST *EQ &TRUE)) DO
             SNDPGMMSG  MSG('FALTA EN DISCO EL ARCHIVO DE +
                          GRABACIONES DE CAMARA') TOPGMQ(*EXT) +
                          MSGTYPE(*INQ)
             RETURN
             ENDDO
 CDE3:
             CALL       PGM(ONLINE/NCHM921L)
             IF         COND(%SWITCH(1XXXXXXX)) THEN(GOTO +
                          CMDLBL(ALFIN))
             CHGJOB     SWS(00000000)

             CALL       PGM(ONLINEC/VERINI) PARM('CAMDEB')
             IF         COND(%SWITCH(1XXXXXXX)) THEN(DO)
             SNDPGMMSG  MSG('Proceso ya ejecutado con anterioridad, +
                          CANCELO') TOPGMQ(*EXT) MSGTYPE(*INQ)
             GOTO       CMDLBL(ALFIN)
             ENDDO
             CHGJOB     SWS(00000000)
             CALL       PGM(ONLINE/CM0937CL)
             CALL       PGM(PREPARFA)
             CHGJOB     SWS(00000000)
             CHGJOB     RUNPTY(15)
             CHGVAR     VAR(&TEST) VALUE(&TRUE)
             CHKOBJ     OBJ(CAMINTWK) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(*NOT (&TEST *EQ &TRUE)) THEN(DO)
         /* SI NO EXISTE LA TABLA QS36F.CAMINTWK  GENERA */
             RUNSQL     SQL('CREATE TABLE QS36F.CAMINTWK AS (SELECT +
                          * FROM CAMINT) WITH NO DATA') COMMIT(*NONE)
             ENDDO
         /* LIMPIA LA TABLA QS36F.CAMINTWK */
             RUNSQL     SQL('TRUNCATE TABLE QS36F.CAMINTWK') +
                          COMMIT(*NONE)
         /* INSERTA DATOS SEGUN EL ORDEN     +
             80  80  OMITE CABECERAS         +
             15  23  - NUMERO DE CUENTA      +
             60  61  - CODIGO DE DEVOLUCION  +
             31  45  - IMPORTE DEL CHEQUE    +
              1  80  TODO EL REGISTRO         */

             RUNSQL     SQL('INSERT INTO QS36F.CAMINTWK SELECT * +
                          FROM CAMINT WHERE SUBSTRING(F00002 FROM +
                          66 FOR 1) = '' '' ORDER BY +
                          SUBSTRING(F00002 FROM  1 FOR  9) ASC, +
                          SUBSTRING(F00002 FROM 46 FOR  2) ASC, +
                          SUBSTRING(F00002 FROM 17 FOR 15) DESC') +
                          COMMIT(*NONE)
/* *                                                                */
             CLRPFM     FILE(CAMAG0)
             OVRDBF     FILE(CAMINT) TOFILE(CAMINTWK)
             CALL       (ONLINEC/CAMPAG)
             DLTOVR     FILE(*ALL)
 REPRO26:
             CLRPFM     FILE(CAMAGX)
             CLRPFM     FILE(CAMERROR)
             OVRDBF     FILE(CAMINT) TOFILE(CAMINTWK)
             CALL       (ONLINEC/CAMPAG1)
             CLRPFM     FILE(CAMINTWK)
             DLTOVR     FILE(*ALL)
             RTVMBRD    FILE(*LIBL/CAMERROR) NBRCURRCD(&RCDNBR)
             IF         COND(*NOT (&RCDNBR *EQ 0)) THEN(DO)
             RUNQRY     QRY(CAMERROR)
             SNDPGMMSG  MSG('EXISTEN CHEQUES ADM. C/PROBLEMAS, +
                          VERIFIQUE SPOOL') TOPGMQ(*EXT) MSGTYPE(*COMP)
             SNDPGMMSG  MSG('ESTOS CHEQUES NO INGRESAN AL PROCESO') +
                          TOPGMQ(*EXT) MSGTYPE(*INQ)
             CPYF       FROMFILE(QS36F/CAMERROR) +
                          TOFILE(QS36F/CAMERROH) MBROPT(*ADD)
             ENDDO
             SNDPGMMSG  MSG('ADICIONA CHEQUES DE CAMARA INTERIOR') +
                          TOPGMQ(*EXT) MSGTYPE(*COMP)
             CALL       ONLINEC/ADDINTE
             SNDPGMMSG  MSG('DETECTA CHEQUES ANULADOS') +
                          TOPGMQ(*EXT) MSGTYPE(*COMP)
             OVRDBF     FILE(SUSTAD) TOFILE(SUSB)
             CALL       PGM(CAMAR11)
             SNDPGMMSG  MSG('COLOCA COD. GERENTE A CAMAG0') +
                          TOPGMQ(*EXT) MSGTYPE(*COMP)
             CALL       PGM(ONLINEC/CAMAR88)
             CALL       PGM(ONLINE/CI0930CL)
             CHGDTAARA  DTAARA(ONLINE/ESTAFOR (1 1)) VALUE('K')
 REPROZ:
             CALL       PGM(ONLINE/CAMAGECL)
             CHGDTAARA  DTAARA(ONLINE/ESTAFOR (1 1)) VALUE('L')
             DLCOBJ     OBJ((QS36F/SOBREGIROS *FILE *EXCL))
             ADDLIBLE   LIB(UTI.LIBR) POSITION(*REPLACE INTERB05)
             UTI.LIBR/SNDASMAIL ADDRESS(CAMARA@itau.com.py) +
                          SUBJECT('Formalística') DATA('Ya se +
                          encuentra disponible el proceso de +
                          rechazos por formalística')
             CLRPFM     FILE(CHENOHAB)
             CALL       PGM(ONLINE/CHENOHAB)
             RUNQRY     QRY(ONLINE/CHENOHAB)
             RUNQRY     QRY(ONLINE/CHENOAG)
             CLRPFM     FILE(QS36F/BDINFOR)
             CPYSPLF    FILE(QPQUPRFIL) TOFILE(QS36F/BDINFOR) +
                          SPLNBR(*LAST)
             CPYTOPCD   FROMFILE(QS36F/BDINFOR) TOFLR(INFORMES) +
                          TODOC(CHENOHA.TXT) REPLACE(*YES)
/*   */
             SNDDST     TYPE(*DOC) TOINTNET((CAMARA@itau.com.py +
                          *PRI)) DSTD('Cheq. en Casa') MSG('Cheques +
                          pendientes de habilitar-Chequera en +
                          Casa') DOC(CHENOHA.TXT) FLR(INFORMES)
             ADDLIBLE   LIB(INTERB05) POSITION(*REPLACE UTI.LIBR)
/* *************************************************************    */
/*  AUTORIZACION MASIVA DE CHEQUES RECHAZADOS COD- 01-04-19-26 *    */
/* *************************************************************    */
             CALL       ONLINE/CAMAR14
             CPYF       FROMFILE(QS36F/CAMAG0) +
                          TOFILE(QS36F/CAMAG0BKE) MBROPT(*REPLACE) +
                          CRTFILE(*YES)
/* *************************************************************    */
 IMPRESO:
             CLRPFM     FILE(RELPGCH2)
             CALL       PGM(ONLINE/RELPAGC2)
             OVRPRTF    FILE(QPQUPRFIL) TOFILE(QPQUPRFIL) +
                          HOLD(*YES) SAVE(*YES)
             RUNQRY     QRY(ONLINE/RELCHEP2)
             CALL       PGM(RELCHEP1)
/* *******CONTROL INHABILITADOS****************                     */
             RUNQRY     QRY(ONLINE/QRYCOD10) QRYFILE(QS36F/CAMAGHB)
/* ************************************************                 */
/*  PARADA PARA REALIZAR CIERRE FORMALISTICA REMOTA                 */
/* ************************************************                 */
             CHGDTAARA  DTAARA(ONLINE/CIESOBR (1 1)) VALUE('H')
/* **ACTUALIZA EN CAMREC RECHAZOS CORE                              */
             CALL       PGM(ONLINE/NCHM911L)
             CALL       PGM(ONLINE/NCHM934L)
             CALL       PGM(ONLINE/NCHM935L)
             CHGJOB     SWS(10000000)
             SNDPGMMSG  MSG('*** EJECUTE CIERRE PROCESO FORMALISTICA +
                          REMOTA ***') TOPGMQ(*EXT) MSGTYPE(*COMP)
             SNDPGMMSG  MSG('*** LUEGO VUELVA A EJECUTAR LA OPCION +
                          C61      ***') TOPGMQ(*EXT) MSGTYPE(*INQ)
             IF         COND(%SWITCH(1XXXXXXX)) THEN(GOTO +
                          CMDLBL(ALFIN))
/* **********************************************                   */
 ARRIBA:
/* ******************************************                       */
/*  RECHAZO DE CHEQUES GENERA CODIGOS 10/44 *                       */
/*  CON LOS RECHAZOS DEL DIA                *                       */
/* ******************************************                       */
             CHGJOB     SWS(00000000)
             CALL       PGM(CAMFRMCL)
             IF         COND(%SWITCH(1XXXXXXX)) THEN(DO)
             SNDPGMMSG  MSG('*** VERIFIQUE CIERRE FORMALISTICA +
                          REMOTA,  ***') TOPGMQ(*EXT) MSGTYPE(*COMP)
             SNDPGMMSG  MSG('*** LUEGO PUEDE CONTINUAR CON ESTE +
                          PROCESO ***') TOPGMQ(*EXT) MSGTYPE(*INQ)
             ENDDO
             CHGJOB     SWS(00000000)
/* **ACTUALIZA EN CAMREC RECHAZOS CORE                              */
             CALL       PGM(ONLINE/NCHM912L)
             CALL       PGM(ONLINE/NCHM924L)
             RUNQRY     QRY(LFORMALI)
             WRKSPLF
             SNDPGMMSG  MSG('VERIFIQUE CON LAS OTRAS PLAZAS SI +
                          TERMINARON SU FORMALISTICA') +
                          TOPGMQ(*EXT) MSGTYPE(*COMP)
             SNDUSRMSG  MSG('DESEAS APLICAR LOS RECHAZOS? (S=SI, +
                          N=VUELVE PANTALLA PARA CORECCION)') +
                          TOMSGQ(*EXT) MSGRPY(&WORK)
             IF         COND(*NOT (&WORK *EQ 'S')) THEN(GOTO +
                          CMDLBL(ARRIBA))
 REPROC:
             OVRDBF     FILE(CAMAGX) TOFILE(CAMAG0)
             CALL       PGM(ONLINE/CAMAR4CL)
 LIST2:
             SNDPGMMSG  MSG('EMITE SEGUNDO LISTADO DE CHEQUES +
                          RECHAZADOS') TOPGMQ(*EXT) MSGTYPE(*COMP)
             CALL       PGM(CAMAGN1CL)
             SNDPGMMSG  MSG('ANULA RECHAZOS AFECTADOS POR RECHAZOS +
                          POR FORMALISTICA') TOPGMQ(*EXT) +
                          MSGTYPE(*COMP)
             CPYF       FROMFILE(QS36F/CAMAG0) +
                          TOFILE(QS36F/CAMAG0AB) MBROPT(*REPLACE) +
                          CRTFILE(*YES)
             CLRPFM     RECHAFOR
             CALL       ANULARE1
             RUNQRY     RECHAFOR
             SNDPGMMSG  MSG('YA PUEDE EMITIR SEGUNDO LISTADO DE +
                          SOBREGIROS') TOPGMQ(*EXT) MSGTYPE(*INQ)
             WRKSPLF
             SNDPGMMSG  MSG('EMITE LISTADO DE LOS CHEQUES RECHAZADOS +
                          EN EL PROCESO') TOPGMQ(*EXT) MSGTYPE(*COMP)
             RUNQRY     CAMAG0
             CPYF       FROMFILE(QS36F/CAMAG0D) TOFILE(QS36F/CAMAGW) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
/* *******************************************************          */
             SNDPGMMSG  MSG('CONTABILIZACION COMISIONES COBRADAS +
                          POR EL SUBSISTEMA DE COMISIONES') +
                          TOPGMQ(*EXT) MSGTYPE(*COMP)
             SNDPGMMSG  MSG(' ') TOPGMQ(*EXT) MSGTYPE(*COMP)
             CHGJOB     SWS(00000000)
             SNDPGMMSG  MSG('ADICIONA LOS ARCHIVOS DE COMISIONES') +
                          TOPGMQ(*EXT) MSGTYPE(*COMP)
             CHGVAR     &TEST (&TRUE)
             CHKOBJ     OBJ(COMISI) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(CPYF +
                          FROMFILE(QS36F/COMISI) +
                          TOFILE(QS36F/COMISIWK) MBROPT(*REPLACE) +
                          CRTFILE(*YES))
             CHGVAR     &TEST (&TRUE)
             CHKOBJ     OBJ(COMISI1) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(CPYF +
                          FROMFILE(QS36F/COMISI1) +
                          TOFILE(QS36F/COMISIWK) MBROPT(*ADD) +
                          CRTFILE(*YES))
             CHGVAR     VAR(&TEST) VALUE(&TRUE)
             CHKOBJ     OBJ(COMISI2) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(CPYF +
                          FROMFILE(QS36F/COMISI2) +
                          TOFILE(QS36F/COMISIWK) MBROPT(*ADD) +
                          CRTFILE(*YES))
             CHGVAR     &TEST (&TRUE)
             CHKOBJ     OBJ(COMISI3) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(CPYF +
                          FROMFILE(QS36F/COMISI3) +
                          TOFILE(QS36F/COMISIWK) MBROPT(*ADD) +
                          CRTFILE(*YES))
             CHGVAR     &TEST (&TRUE)
             CHKOBJ     OBJ(COMISI4) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(CPYF +
                          FROMFILE(QS36F/COMISI4) +
                          TOFILE(QS36F/COMISIWK) MBROPT(*ADD) +
                          CRTFILE(*YES))
             CHGVAR     &TEST (&TRUE)
             CHKOBJ     OBJ(COMISI5) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(CPYF +
                          FROMFILE(QS36F/COMISI5) +
                          TOFILE(QS36F/COMISIWK) MBROPT(*ADD) +
                          CRTFILE(*YES))
             CHGVAR     &TEST (&TRUE)
             CHKOBJ     OBJ(COMISI6) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(CPYF +
                          FROMFILE(QS36F/COMISI6) +
                          TOFILE(QS36F/COMISIWK) MBROPT(*ADD) +
                          CRTFILE(*YES))
             CHGVAR     &TEST (&TRUE)
             CHKOBJ     OBJ(COMISI7) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(CPYF +
                          FROMFILE(QS36F/COMISI7) +
                          TOFILE(QS36F/COMISIWK) MBROPT(*ADD) +
                          CRTFILE(*YES))
             CHGVAR     &TEST (&TRUE)
             CHKOBJ     OBJ(COMISI8) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(CPYF +
                          FROMFILE(QS36F/COMISI8) +
                          TOFILE(QS36F/COMISIWK) MBROPT(*ADD) +
                          CRTFILE(*YES))
             OVRDBF     FILE(COMISI) TOFILE(COMISIWK)
             OVRDBF     FILE(CTESDO) TOFILE(CTESDO)
             OVRDBF     FILE(CAHSDO) TOFILE(CAHSDO)
             OVRDBF     FILE(TABIVA) TOFILE(TABIVA)
             OVRDBF     FILE(MOCO) TOFILE(MOCOB)
             OVRDBF     FILE(TOL) TOFILE(TOLB)
             CALL       PGM(COMIS3NW)
             CLRPFM     FILE(COMISIWK)
             DLTOVR     FILE(*ALL)
             CPYF       FROMFILE(QS36F/PENDX) TOFILE(SOFTSHOP/PENDX) +
                          MBROPT(*REPLACE) CRTFILE(*YES)
             CALL       VERAGECL PARM(&USER)
             CHGVAR     VAR(&PM07) VALUE(((%SST(*LDA 035 002))))
             CALL       LISCAMCL
             CALL       COL01TN
 SALTA:
             CHGDTAARA  DTAARA(ONLINE/ESTAFOR (1 1)) VALUE('M')
             CALL       ONLINE/PROFOR01B
 ALFIN:

             ENDPGM
