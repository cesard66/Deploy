             PGM

             DCL        VAR(&DATE) TYPE(*CHAR) LEN(006)
             DCL        VAR(&RCDNBR) TYPE(*DEC) LEN(010 0)
             DCL        VAR(&TEST) TYPE(*CHAR) LEN(001)
             DCL        VAR(&TRUE) TYPE(*CHAR) LEN(001) VALUE('1')

             SNDPGMMSG  MSG('Selecciona sobregiros devengados que +
                          se pueden cobrar') TOPGMQ(*EXT) +
                          MSGTYPE(*COMP)
/* *** CORRE TODOS LOS D√çAS, EXCEPTO FIN DE MES ****                */

             CHGJOB     SWS(00000000)
             RTVJOBA    DATE(&DATE)
             CHGDTAARA  DTAARA(*LDA (1 6)) VALUE(&DATE)
             CALL       PGM(GLOBAL/ACTIUX)
             IF         COND(%SWITCH(X1XXXXXX)) THEN(GOTO +
                          CMDLBL(ALFIN))
             CHGJOB     SWS(00000000)
             CLRPFM     FILE(SOBRESP)
             CALL       PGM(INTERB01/SOBDEVO2)
             RTVMBRD    FILE(QS36F/SOBRESP) NBRCURRCD(&RCDNBR)

             IF         COND(&RCDNBR *EQ 0) THEN(GOTO CMDLBL(ALFIN))
             SNDPGMMSG  MSG('Preparo archivo para meter en el +
                          proceso de Cuentas Corrientes') +
                          TOPGMQ(*EXT) MSGTYPE(*COMP)

             CHGVAR     VAR(&TEST) VALUE(&TRUE)
             CHKOBJ     OBJ(CTEF16W) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(DLTF +
                          FILE(QS36F/CTEF16W))
             RUNSQL     SQL('CREATE TABLE QS36F.CTEF16W LIKE +
                          QS36F.SOBRESP') COMMIT(*NONE)
             MONMSG     MSGID(CPF0000)
             RUNSQL     SQL('INSERT INTO QS36F.CTEF16W SELECT * FROM +
                          QS36F.SOBRESP ORDER BY SKEY ASC') +
                          COMMIT(*NONE)

             CHGVAR     VAR(&TEST) VALUE(&TRUE)
             CHKOBJ     OBJ(SOBRESPW) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR &TEST ('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(DLTF +
                          FILE(QS36F/SOBRESPW))
             RNMOBJ     OBJ(CTEF16W) OBJTYPE(*FILE) NEWOBJ(SOBRESPW)
             SNDPGMMSG  MSG('   Impresion de los gastos de +
                          Sobregiros') TOPGMQ(*EXT) MSGTYPE(*COMP)
             SNDPGMMSG  MSG('   Genera el archivo SGIRO para +
                          ingresar en transacciones diarias.') +
                          TOPGMQ(*EXT) MSGTYPE(*COMP)
             CHGVAR     &TEST (&TRUE)
             CHKOBJ     OBJ(SGIRO) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR VAR(&TEST) +
                          VALUE('0'))
             IF         COND(&TEST *EQ &TRUE) THEN(DLTF +
                          FILE(QS36F/SGIRO))
             OVRDBF     FILE(SOBRE) TOFILE(SOBRESPW)
             OVRPRTF    FILE(CTES30) FORMTYPE(RE66) COPIES(1) +
                          HOLD(*YES)
             CALL       PGM(CTES30NW)
             DLTOVR     FILE(*ALL)
             CRTPF      FILE(QS36F/POSCCCME) RCDLEN(80) SIZE(*NOMAX)
/* LABEL-SOBRESPW                                                   */
             OVRPRTF    FILE(CTES30) FORMTYPE(RE66) COPIES(1) +
                          HOLD(*YES)
             CALL       PGM(CTES30ENW)

             RTVMBRD    FILE(POSCCCME) NBRCURRCD(&RCDNBR)
             IF         (&RCDNBR *EQ 0) DLTF FILE(POSCCCME)
             RTVMBRD    FILE(POSCCCME) NBRCURRCD(&RCDNBR)
             IF         (*NOT (&RCDNBR *EQ 0)) DO
             PAUSE      MSG('REVISAR POSICION DE CAMBIOS SOBR.DIARIO')
             ENDDO
/* ****************** OPERACIONES CONTABLES ******************      */
             SNDPGMMSG  MSG('Genera transacciones para +
                          contabilidad') TOPGMQ(*EXT) MSGTYPE(*COMP)
             OVRDBF     FILE(BDBRREL1) TOFILE(BDIGEN/BDBRREL1)
             OVRDBF     FILE(BDBRREL0) TOFILE(BDIGEN/BDBRREL0)
             OVRDBF     FILE(BDBTREL0) TOFILE(BDIGEN/BDBTREL0)
             OVRDBF     FILE(BDBSREL0) TOFILE(BDIGEN/BDBSREL0)
             OVRDBF     FILE(INAUREL2) TOFILE(BDIGEN/INAUREL2)
             OVRDBF     FILE(BDCEREL0) TOFILE(BDIGEN/BDCEREL0)
             OVRDBF     FILE(UCGMACL7) TOFILE(ITBDBLIB/UCGMACL7)
             OVRDBF     FILE(BDCFREL0) TOFILE(BDIGEN/BDCFREL0)
             OVRDBF     FILE(BDBQREL2) TOFILE(BDIGEN/BDBQREL2)
/* * Moneda Local                                                   */
             OVRDBF     FILE(SOBRE) TOFILE(QS36F/SOBRESPW)
             CALL       PGM(INGEN/TR63CCT)
             CALL       PGM(INGEN/TR63NCCT)
/* * Moneda Extranjera                                              */
             CALL       PGM(INGEN/TR63CCME)
             CALL       PGM(INGEN/TR63NCCME)
/* * LLEVA AL POPULAR FAMAFA                                        */
             CALL       INTERB01/FAMASOL1
             CALL       INTERB01/FAMASOE1
             DLTOVR     FILE(*ALL)
             CHGVAR     &TEST (&TRUE)
             CHKOBJ     OBJ(SOBRESPW) OBJTYPE(*FILE)
             MONMSG     MSGID(CPF9801) EXEC(CHGVAR &TEST ('0'))
             IF         (&TEST *EQ &TRUE) DLTF FILE(SOBRESPW)
 ALFIN:
/* *                                                                */

             ENDPGM
