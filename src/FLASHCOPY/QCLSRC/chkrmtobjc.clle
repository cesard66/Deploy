/******************************************************************************/
/* Programa: CHKRMTOBJC                                                       */
/* CPP de  : CHKRMTOBJ                                                        */
/* Funcion : Verifica si existe objeto remoto                                 */
/* Hecho   : Ino                                                              */
/* Parametros:                                                                */
/*            USUARIO  : Usuario remoto                                       */
/*            PASSWORD : Password                                             */
/*            CMD      : CMD a ejecutar                                       */
/*            IP       : IP remoto                                            */
/*            Error    : Devuelve '0' existe objeto remoto                    */
/*                           '1' no existe objeto remoto                      */

/* Programas invocados                                                        */
/* -------------------                                                        */
/* RUNCMDRPG                                                                  */
/* READERRCMD                                                                 */

/* OBSERVACION:                                                               */
/******************************************************************************/
PGM   PARM(&USR &PWD &CMD &IP &ERROR)
      DCL VAR(&USR)    TYPE(*CHAR) LEN(10)
      DCL VAR(&PWD)    TYPE(*CHAR) LEN(10)
      DCL VAR(&CMD)    TYPE(*CHAR) LEN(2000)
      DCL VAR(&IP)     TYPE(*CHAR) LEN(32)
      DCL VAR(&JOBNBR) TYPE(*CHAR) LEN(6)
      DCL VAR(&SRCPFI) TYPE(*CHAR) LEN(10)
      DCL VAR(&SRCPFO) TYPE(*CHAR) LEN(10)
      DCL VAR(&SYSCMD) TYPE(*CHAR) LEN(10)
      DCL VAR(&ERR)    TYPE(*CHAR) LEN(1)
      DCL VAR(&ERROR)  TYPE(*CHAR) LEN(1)

/* 1-Crea archivo QTEMP/FTPENVB con los sgtes.miembros:                       */
/*   SRCI+JOBNBR: Input (Se graban sentencias FTP)                            */
/*   SRCO+JOBNBR: Output (Se graban los mensajes de FTP)                      */
/* 2-El prog.RPG RUNCMDRPG graba las sentencias que estan en &CMD en el arc.  */
/*   QTEMP/FTPENVB miembro SRCI+JOBNBR (&CMD)                                 */
/* 3-Se ejecutan las sentencias FTP (StrTcpFtp)                               */
/* 4-El prog. READERRCMD lee QTEMP/FTPENVB miembro SRCO+JOBNBR para im-       */
/*   primir los errores (si hay errores)                                      */

/* 1-Crea archivo QTEMP/FTPENVB con los sgtes.miembros:                       */
/*   'SRCI' + &JOBNBR                                                         */
/*   'SRCO' + &JOBNBR                                                         */

      DLTF FILE(QTEMP/FTPENVB)
      MONMSG MSGID(CPF2105)

      CRTSRCPF FILE(QTEMP/FTPENVB) RCDLEN(300)

      RTVJOBA NBR(&JOBNBR)
      CHGVAR VAR(&SRCPFI) VALUE('SRCI' *CAT &JOBNBR)
      CHGVAR VAR(&SRCPFO) VALUE('SRCO' *CAT &JOBNBR)
      ADDPFM FILE(QTEMP/FTPENVB) MBR(&SRCPFI)
             MONMSG MSGID(CPF5812)
      ADDPFM FILE(QTEMP/FTPENVB) MBR(&SRCPFO)
             MONMSG MSGID(CPF5812)

/* 3-Se graban y ejecutan las sentencias FTP                                  */

         CLRPFM FILE(QTEMP/FTPENVB) MBR(&SRCPFI)
         CLRPFM FILE(QTEMP/FTPENVB) MBR(&SRCPFO)

         OVRDBF FILE(FTPENV) TOFILE(QTEMP/FTPENVB) MBR(&SRCPFI)
         CALL PGM(*LIBL/RUNCMDRPG) PARM(&USR &PWD &CMD)

         OVRDBF FILE(INPUT) TOFILE(QTEMP/FTPENVB) MBR(&SRCPFI)
         OVRDBF FILE(OUTPUT) TOFILE(QTEMP/FTPENVB) MBR(&SRCPFO)
             STRTCPFTP  RMTSYS(&IP) PORT(*SECURE) SECCNN(*IMPLICIT)

/* 4-El prog.READERRCMD lee QTEMP/FTPENVB miembro SRCO+JOBNBR para im-        */
/*   primir los errores (si lo hubiere)                                       */

    SNDPGMMSG MSG('Imprimiendo errores')
    CHGVAR VAR(&ERROR) VALUE('0')
    CHGVAR VAR(&ERR)   VALUE('0')
    DLTOVR FILE(*ALL)
    OVRDBF FILE(FTPENV) TOFILE(QTEMP/FTPENVB) MBR(&SRCPFO)
    OVRDBF FILE(QPRINT) TOFILE(QGPL/QPRINT)
    CALL PGM(*LIBL/READERRCMD) PARM(&ERR)
    MONMSG MSGID(CPF0000)
    CHGVAR vAR(&ERROR) VALUE(&ERR)

 END: ENDPGM
